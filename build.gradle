plugins {
	id 'java'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '5.2.0'
}

repositories {
	mavenCentral()
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
	compileOnly group: 'org.ow2.asm', name: 'asm-debug-all', version: '5.2'
	runtimeOnly group: 'org.ow2.asm', name: 'asm-all', version: '5.2'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
	options.encoding = 'UTF-8'
}

jar {
	manifest {
		attributes 'Main-Class': 'immibis.bon.gui.GuiMain'
	}
}

shadowJar {
	relocate 'org.objectweb.asm', 'immibis.bon.org.objectweb.asm'
}

task sourcesJar(type: Jar) {
	archiveClassifier.set('sources')
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	archiveClassifier.set('javadoc')
	from javadoc
}

/* Task dependency */
tasks.assemble.finalizedBy tasks.shadowJar

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}
}